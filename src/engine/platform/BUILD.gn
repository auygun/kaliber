config("platform_public") {
  defines = [ "USE_GLFW" ]
}

source_set("platform") {
  public_configs = [ ":platform_public" ]

  sources = [
    "asset_file.cc",
    "asset_file.h",
    "platform.h",
    "platform_observer.h",
  ]

  libs = []
  deps = [ "//src/base" ]
  public_deps = []

  if (target_os == "linux" || target_os == "win") {
    sources += [ "asset_file_generic.cc" ]
  }

  if (target_os == "linux") {
    # sources += [ "platform_linux.cc" ]
    sources += [ "platform_glfw.cc" ]
    libs += [
      "X11",
      "GL",
    ]
    public_deps += [ "//src/third_party/glfw" ]
  } else if (target_os == "win") {
    sources += [ "platform_win.cc" ]
    libs = [
      "gdi32.lib",  # Graphics
      "user32.lib",  # Win32 API core functionality.
      "Ole32.lib",  # COM
      "opengl32.lib",
    ]
  } else if (target_os == "android") {
    sources += [
      "asset_file_android.cc",
      "platform_android.cc",
    ]
    libs += [
      "android",
      "EGL",
      "GLESv3",
      "log",
      "z",
    ]
    deps += [
      "//src/third_party/android",
      "//src/third_party/minizip",
    ]
  }
}
